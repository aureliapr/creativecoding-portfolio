"Strange Bedroom" by Aurelia Peterson Rajalingam

Release along with an interpreter, an "Iridium" website, and the source text.


Chapter 1 - Setting Up Time Limit

The time limit is a number that varies. The time limit is 30.

When play begins:
	say "WARNING: You have [time limit] minutes to escape the room before you are stuck forever";
	now the right hand status line is "[time limit] minutes remaining".
	
Every turn:
	decrease the time limit by 1;
	if the time limit is greater than 0:
		if the time limit is 1:
			say "WARNING: Only 1 minute remaining!";
		otherwise:
			say "[time limit] minutes remaining...";
	otherwise:
		end the story saying "The containment protocols have activated. You are trapped forever."

Chapter 2 - Conversation Rules

Interrogative is a kind of value. 
The interrogatives are who, what, when, where, how, and why.

Current question is an interrogative that varies.

After reading a command:
	if the player's command includes "[interrogative]":
		now the current question is the interrogative understood.		

Understand "ask [someone] [text]" as asking it about.

Chapter 3 - The Bedroom

The Bedroom is a room. "You are in a small, cluttered bedroom. There are posters on the walls and various toys scattered about. The room is dark and cold, with thick vines covering the posters on the walls. A large window looks out onto an increasingly unsettling sky. East is the kitchen. You see a small boy named Will sitting on the bed in front of the window."

The window is scenery in the Bedroom. The description of the window is "The window pane is freezing to the touch, and through it, you can see something is very wrong with the sky. Dark clouds cover the sky. A crimson mist hovers in the distance. Huge white particles hang in the air and smoke-like shapes writhe in the distance. The longer you look, the more unsettling the shapes become." Understand "look at [something]" as examining.

A poster is scenery in the Bedroom. The description is "Various movie and comic book posters cover the walls." Understand "posters" as a poster.

Some toys are scenery in the Bedroom. The description is "Board games are scattered around the room and paper and crayons are splayed across the floor" 


Chapter 5 - Will 

Will is a person in the Bedroom. Will can be greeted or ungreeted. Will is ungreeted. The description of Will is "A lanky, sensitive child with a distant look in his eyes. His sketchbook is always nearby, filled with drawings that seem to hint at darker, more mysterious worlds."

Greeting is an action applying to one visible thing.
Understand "hello [someone]" as greeting.
Understand "hi [someone]" as greeting.
Understand "hey [someone]" as greeting.
Understand "greet [someone]" as greeting.
Understand "say hello to [someone]" as greeting.
Understand "say hi to [someone]" as greeting.
Understand "say hey to [someone]" as greeting.

Instead of greeting Will when Will is ungreeted:
	now Will is greeted;
	say "'Oh... hey,' Will says softly, looking up from his sketchbook with a mix of nervousness and curiosity. 'Sorry, I was just drawing something. Did you want to, um, talk?'"

Instead of greeting Will when Will is greeted:
	say "You've already greeted Will."

Instead of asking Will about something when Will is ungreeted:
	say "Maybe you should say hello to Will first? He seems nervous around strangers."

After asking Will about something when Will is greeted:
	respond to the query.
	
Table of Will's Answers
topic								question type	reply
"dnd/d&d/dungeons/dragons"			what			"'Okay, this is weird,' Will says nervously, adjusting his shirt. 'D&D is like... a way to escape reality, you know? Where monsters aren't real monsters, and you can actually control what happens to you.'"
"dnd/d&d/dungeons/dragons"			how				"'I usually play as the Dungeon Master,' Will admits with a shy smile. 'It lets me create worlds where I'm in control - something I don't always feel in real life. You pick a character, roll some dice, and suddenly you're somewhere else entirely.'"
"drawing/art/sketching"				what			"'Drawing is how I make sense of things,' Will says softly, his fingers unconsciously tracing an imaginary sketch in the air. 'Sometimes words aren't enough, especially when you've seen the things I've seen.'"
"drawing/art/sketching"				why				"'It helps me process,' Will explains, his voice quiet but intense. 'After everything that's happened, drawing is like... a way to understand the monsters, to make them less scary by putting them on paper.'"
"upside down/alternate dimension"	what			"'It's not just a place,' Will says, a haunted look crossing his face. 'It's like a nightmare you can't wake up from. Cold, dark, and always watching. Trust me, you don't want to know more.'"
"upside down/alternate dimension"	how				"'Sometimes the dimensions bleed into each other,' Will whispers, looking over his shoulder. 'And when they do, nothing makes sense anymore. The rules you thought you knew? They don't apply.'"
"being/feeling different"			who				"'I'm just... different,' Will says with a mix of vulnerability and defiance. 'And that's okay. Different doesn't mean broken. It just means I've seen things others haven't - and survived.'"
"mike/friendship/friends"			who				"'Mike's more than a friend,' Will says softly. 'He's like... family. The kind that stands by you, even when the world seems to be falling apart around you.'"
"supernatural/weird experiences"	what			"'Weird is kind of my normal,' Will says with a wry, slightly tired smile. 'When you've been where I've been, supernatural stops being shocking and starts being just another Tuesday.'"
"hopper/jim"						who				"'Hopper's complicated,' Will says thoughtfully. 'He's tough on the outside, but he'd do anything to protect the people he cares about. Especially El. He became more than just a cop - he became a dad.'"
"angela"							who				"'Angela...' Will's face darkens. 'Some people are just cruel for no reason. She made me feel small, different. But I learned that their opinions don't define who I am.'"
"murray/bauman"					who				"'Murray's kind of crazy - but in a smart way,' Will chuckles softly. 'He sees connections others miss. He might seem paranoid, but after everything I've been through, paranoia can keep you alive.'"
"nancy/wheeler"					who				"'Nancy's brave,' Will says with respect. 'She doesn't just sit back when something's wrong. She investigates, she fights. She's the kind of person who turns fear into action.'"
"jonathan/byers"					who				"'Jonathan's my brother,' Will says, a mix of admiration and vulnerability in his voice. 'He's always protected me. Understood me when no one else did. We've been through so much together.'"
"eleven/el/jane"					who				"'El is... special,' Will explains, a protective tone in his voice. 'She's been through so much, but she's the strongest person I know. Not just because of her powers, but because of her heart.'"
"dustin/henderson"					who				"'Dustin? He's like this weird science genius,' Will chuckles softly. 'Always talking about compasses and theories. But he's loyal, you know? When things get weird, he's the first one with a crazy plan that just might work.'"
"lucas/sinclair"					who				"'Lucas is the most practical of us,' Will says with a hint of admiration. 'He thinks things through. Where I get lost in my drawings or my fears, Lucas is always looking for a real solution.'"
"max/maxine"						who				"'Max is tough,' Will says, a smile crossing his face. 'She doesn't take crap from anyone. After everything that happened with Billy, she's had to be strong in ways most people couldn't understand.'"
"joyce/mom"						who				"'My mom... she never gave up on me,' Will says, his voice cracking slightly. 'Even when everyone else thought I was crazy or lost, she fought. She always fights for her kids.'"
"supernatural/weird experiences"	what			"'Weird is kind of my normal,' Will says with a wry, slightly tired smile. 'When you've been where I've been, supernatural stops being shocking and starts being just another Tuesday.'"
"trauma/survival"					how				"'You learn to adapt,' Will says quietly. 'Some experiences change you forever. But surviving isn't just about making it out alive. It's about finding hope in the darkest places.'"
"location/place/here/where we are"		where			"'We're in my house,' Will says nervously, 'but not really. It's like a dark reflection - the Upside Down version. Everything here is wrong, twisted. The air itself feels... hostile.'"
"location/place/here/where we are"		what			"'This place... it's like a nightmare version of Hawkins,' Will explains, shuddering. 'The same buildings, the same rooms, but everything's cold, dark, and covered in these weird vines. It's like looking at the world through a broken mirror.'"
"escape/way out/getting out"			how				"'There might be a way,' Will whispers, glancing around. 'Dustin's compass and Lucas's wrist rocket - together they might be strong enough to open a portal. The compass reacts to dimensional gates, and the wrist rocket... well, Lucas modified it in ways even he doesn't understand.'"
"escape/way out/getting out"			where			"'The living room,' Will says quietly. 'Mom set up those Christmas lights to communicate when I was trapped here. There's usually a weak spot there, where the dimensions are thin. If you can find a way to activate it...'"
"portal/gate/tear"					what			"'The portal is like a tear between dimensions,' Will explains, his voice tense. 'When it's active, it glows and pulses with this strange energy. But you need something to power it - either special objects or...' he trails off."
"portal/gate/tear"					how				"Will glances between the ghost trap and the kitchen. 'You'll need tools to open it. Dustin's compass is nearby - it can sense dimensional anomalies. And Lucas left his wrist rocket somewhere in the kitchen. Together they might be enough.'"
"radio/music"						what			"Will's eyes widen with recognition. 'That old radio... it's not just for music. Channel 11 especially.'"
"radio/music"						how				"'The radio has two channels,' Will says hesitantly. 'Channel 5 is usually just static, but 11... that's where strange things happen. If you're brave enough, it might open another way out. '"
"compass/dustin's compass"			where			"'Dustin's compass should be in that ghost trap in the living room,' Will says. 'It goes crazy whenever a portal is nearby.'"
"wrist rocket/slingshot"				where			"'Lucas's wrist rocket? Last I saw it, it was on the kitchen counter,' Will recalls. 'He's really protective of it.'"
"time/hurry/rush"					what			"'You don't have much time,' Will warns urgently. 'The longer you stay here, the stronger its hold becomes. Once the containment protocols activate... that's it. You're trapped like I was.'"
"vecna/curse/monster"				who				"Will pales at the mention. 'Vecna is... he gets in your head. Uses your memories against you. But sometimes,' he adds thoughtfully, 'music can be turned against him.'"
"help/hint/stuck"					how				"'Look,' Will says, leaning forward, 'you have two ways out. Find Dustin's compass and Lucas's wrist rocket - together they can open the portal. Or...' he glances at the radio, 'there's channel 11, but that's risky. Vecna might be listening.'"
"upside down/escape"				why				"'The Upside Down doesn't like visitors,' Will says grimly. 'It'll try to keep you here, like it did with me. That's why you need to hurry - find the items or risk the radio, but don't let time run out.'"
"window/sky/view"						what			"'The sky...' Will shudders, looking out. 'It's always wrong here. Those particles floating around? We call them spores. And those shapes in the distance... sometimes I draw them, but I try not to look too long.'"
"window/sky/view"						why				"'The sky looks like that because this place is dying,' Will explains softly. 'Or maybe it was never really alive. The clouds, the red mist... it's like the air itself is toxic. That's why we need to get you out of here.'"
"toys/games/board games"				what			"Will glances at the scattered toys with a mix of nostalgia and sadness. 'Those are from before... everything. D&D manuals, my old Castle Byers drawings, some comics Mike and I used to read together. They feel like they're from another lifetime now.'"
"toys/games/board games"				where			"'The toys are all over my room,' Will says, gesturing around. 'Mom never moved them, even after... you know. I think she wanted everything to stay exactly the same, just in case I came back.'"
"posters/walls"						what			"'Those posters,' Will says, looking at the vine-covered walls, 'they're from movies and comics I loved. The Thing, Star Wars... but here, the vines are slowly consuming them. Like this place is trying to erase who I was.'"
"posters/walls"						how				"'The vines grow over everything eventually,' Will explains, watching the tendrils creep across his wall decorations. 'They're like the veins of this place. Sometimes I think they're alive, watching us.'"
"bed/bedroom"						what			"Will shifts uncomfortably on the bed. 'This room used to be my safe place. Now it's just another reminder of how everything's changed. The bed's always cold here, like it's trying to drain the warmth from you.'"
"vines/tendrils"						what			"'The vines are everywhere,' Will says, his voice dropping to a whisper. 'They're connected to something... bigger. Something that doesn't want us here. They grow, they spread, they consume everything eventually.'"
"crayons/paper/drawings"				what			"Will picks up a crayon, turning it over in his hands. 'I used to draw normal things - rockets, knights, adventures. Now I can only draw what I see here. The monsters, the decay, the darkness... it's like my art got infected too.'"
"sketchbook/drawings"				what			"'My sketchbook...' Will hesitates, clutching it closer. 'It's full of things I've seen here. Things I can't explain in words. Sometimes drawing them is the only way to make sense of it all, even if they give me nightmares.'"
"castle byers/fort"					what			"A faint smile crosses Will's face. 'Castle Byers was my fortress. But nothing stays safe here for long. The darkness finds everything eventually.'"
"room/bedroom/space"					how				"'Everything in here has a shadow version,' Will explains, looking around his room. 'It's like a dark echo of my real bedroom. The same stuff, but twisted, cold, wrong. Even the air feels different - heavier, like it's watching you.'"
"comics/books"						what			"Will picks up a mold-covered comic book. 'X-Men, Wizards & Warriors... Mike and I used to read these for hours. Here they're all decaying, like everything else. But sometimes I still look at them, just to remember.'"
"light/lighting/dark"					what			"'It's always dark here,' Will says, glancing around his dim bedroom. 'Not just normal dark - it's like the darkness is alive, trying to swallow every bit of light. That's why Mom put up those Christmas lights in the living room.'"

To respond to the query:
	repeat through the Table of Will's Answers:
		if the topic understood includes topic entry:
			if the current question is the question type entry:
				say "[reply entry][paragraph break]";
				rule succeeds;

Chapter 6 - Swearing at Will 

Table of Swears
swear_word    
"fuck"
"shit"
"bastard"
"bitch"
"asshole"
"damn"
"hell"
"crap"
"dammit"
"bullshit"
"goddamn"
"frick"
"idiot"
"stupid"
"dumb"
"moron"
"jerk"


Instead of asking Will about something:
	let current command be "[the player's command]" in lower case;
	repeat through Table of Swears:
		let bad word be "[swear_word entry]";
		if current command matches the text bad word:
			say "A strange, cold smile spreads across Will's face. 'You have no idea what you're messing with,' he mutters. 'Vecna is always listening. And now, he's found you.'";
			say "Will's face contorts with something dark. His eyes go black as he whispers, 'I have a special connection with Vecna. You shouldn't have said that.'";
			end the story saying "Suddenly, everything around you goes cold. The room twists and warps as Vecna's presence fills your mind. You feel your body seize as shadows grip you, the darkness swallowing you whole. Your last breath is taken in terror. You die a gruesome death, consumed by Vecna's power.";
			rule succeeds;
	continue the action.
	
Chapter 7 - Kitchen

The Kitchen is east of the Bedroom. The description is "The kitchen is bare except for a fridge, a countertop with a toaster on top, and a table in the center of the room. North of the kitchen is the front door. The front door is locked tight for now. East of the kitchen is the living room."

The refrigerator is an openable closed container in the Kitchen. In the refrigerator is a can of Coca-Cola. Understand "fridge" as the refrigerator. The description of the refrigerator is "Its once fresh white coat is faded brown and it is covered in vines, but a handle is visible under the mess."

The countertop is a supporter in the Kitchen. It is fixed in place. The description of the countertop is "On the countertop sits a toaster, two freshly popped eggos sit waiting in the toaster. A wrist rocket sits on the countertop." The eggos are an edible thing on the countertop.

Lucas's wrist rocket is a thing on the countertop. The description is "Lucas's signature weapon - a powerful slingshot he's modified for maximum accuracy and power. The metal frame seems to hum strangely in this dimension." 

The front door is scenery in the Kitchen. Instead of opening or entering the front door, say "It's locked tight."
	
Chapter 8 - Alternate Death Eggos

Instead of eating the eggos:
	say "As you take a bite, a strange, metallic taste spreads across your tongue. The eggos are laced with a dark, viscous substance. Moments later, you feel your body begin to convulse, dark tendrils seeming to grow from inside you...";
	end the story saying "The contaminated Eggos have claimed another victim. The Upside Down's insidious poison slowly consumes your body."


Chapter 9 - The Living Room

The Living Room is east of the Kitchen. The description of the Living Room is "You see a couch, a table and a TV. Dustin Henderson sits on the couch, multicolor fairy lights are arranged in rows above it with letters of the alphabet written in black paint above each bulb. You realize you are in the Upside Down!! You are in Joyce Byers's house, you need to get out before the time is up or you will be stuck in the Upside Down forever! You see an interdimensional portal on the ceiling above the lights and a ghost trap lying on top of the desk in front of the couch. A vintage radio sits on a shelf, covered in a light layer of dust."

The desk is a supporter in the Living Room. It is fixed in place. The ghost trap is on the desk. The ghost trap is an openable closed container on the desk. The description of the ghost trap is "The ghost has a button on it, who knows what you might find inside?" In the ghost trap is Dustin's Compass. Dustin's compass is a thing in the ghost trap. The description is "Dustin's trusty compass. In the real world, it spins wildly near interdimensional gates - maybe here it could help locate a way home." 

Understand "Dustin's compass" or "compass" or "dustins compass" or "Dustin's Compass" as Dustin's compass.

Understand "Lucas's wrist rocket" or "Lucas's Wrist Rocket" or "wrist rocket" or "slingshot" or "lucas wrist rocket" or "lucas's slingshot" or "lucas slingshot" as Lucas's wrist rocket.


The portal is scenery in the Living Room. The portal can be active or inactive. The portal is inactive. The description is "[if active]The portal pulses with mystical energy, creating a doorway to our world.[otherwise] The portal is currently dormant, waiting to be activated, find Dustin's Compass and Lucas's Wristwrocket to open the portal." 

Chapter 10 - Dustin 

The stump-counter is a number that varies.
The stump-counter is 0.

After asking Dustin about something: 
	respond to the question.
	
Dustin is a person in the Living Room. The description of Dustin is "A curly-haired teenager wearing his signature cap and sporting an infectious grin. His backpack appears to be stuffed with various scientific equipment and at least three D&D sourcebooks."

Instead of greeting Dustin:
	say "'Hey hey, no need for formalities!' Dustin waves off your greeting with a grin. 'I already gave you the welcome speech when you walked in. Now, what do you want to know about interdimensional anomalies?'"
	

Table of Dustin's Answers
topic								question type	reply
"dnd/d&d/dungeons/dragons"			what			"'Oh man, you're asking about the greatest game ever invented!' Dustin exclaims, adjusting his cap excitedly. 'It's this amazing tabletop role-playing game where you can be anyone - a powerful wizard, a sneaky rogue, even a totally tubular paladin! And the dice, don't get me started about the dice!'"
"dnd/d&d/dungeons/dragons"			how				"'First thing you gotta know is your character class. I personally love playing as a Bard because, you know, these pearls aren't just for show!' Dustin grins, pointing to his teeth. 'Then you roll, and boom - the adventure begins!'"
"dice/rolling/d20"					what			"'The sacred geometry of chance!' Dustin pulls out a handful of multi-colored dice. 'You've got your d4s, d6s, d8s, d10s, d12s, and the holy grail - the d20! Each one's got its own personality.'"
"dice/rolling/d20"					how				"'Listen up because this is crucial,' Dustin says, demonstrating with a flourish. 'You gotta respect the dice. Roll them with confidence, but never - and I mean NEVER - use a dice that's rolled three nat 1s in a row.'"
"dice/rolling/d20"					why				"'Why do we use dice? Because chaos is beautiful' Dustin exclaims. 'Sometimes you roll a nat 20 and slay the dragon, sometimes you roll a 1 and fall flat on your face. That's what makes it exciting!'"
"upside down/alternate dimension"	what			"'Son of a bitch, you're asking about serious stuff here,' Dustin whispers, leaning in close. 'It's like our world but darker, colder, and way more dangerous. Think of it as a dark reflection - a parallel dimension of our world.'"
"upside down/alternate dimension"	how				"Dustin pulls out his compass, handing it to you. 'The electromagnetic field goes totally bonkers when dimensional gates are nearby affecting my trust compass. But seriously, let's hope you never have to find out firsthand!'"
"science/physics/chemistry"			what			"'Science?' Dustin's eyes light up. 'It's like real-world magic, but instead of spells, we've got equations! And instead of wizards, we've got guys like Mr. Clarke. The best teacher by the way!'"
"science/physics/chemistry"			why				"'Because science RULES!' Dustin throws his hands up enthusiastically. 'How else would we understand electromagnetic fields, or build our awesome radio tower, or figure out why compasses go crazy near interdimensional portals?'"
"dustin/yourself/he"					who				"'I'm Dustin Henderson, master of science, D&D enthusiast extraordinaire, and proud owner of these pearly whites!' He grins broadly, showing off his teeth. 'Some say I'm the brains of the party, but really, I'm just a guy who appreciates a good adventure and the scientific method.'"
"monsters/creature/demogorgon"		what			"'Oh man, oh man, you're asking about some serious stuff here,' Dustin says, voice dropping to a whisper. 'Let's just say there are things out there that would make even the toughest D&D monster look like a baby Yoda. And trust me, I've seen them!'"
"mews/cat/kitty"					who				"'Mews? Oh man, she was the most amazing cat ever!' Dustin's face falls slightly, his voice catching. 'She had this crazy sixth sense about supernatural stuff, always knew when something was off. And, man, she really loved my purring—no one else could get it like I could. I miss her so much... It's like the house is just... empty without her.'"
"mews/cat/kitty"					what			"'Mews was more than just a cat, she was a part of the family,' Dustin says quietly, his eyes momentarily distant. 'She had these crazy whiskers that could detect changes in the electromagnetic field—at least, that's what I thought. I never got the chance to finish my tests on her... It's just not the same without her here, you know? It's like the whole house lost its spark when she... when she passed.'"
"mike/wheeler"						who				"'Mike? He's our Paladin, our fearless leader!' Dustin says proudly. 'Sure, he can be a real pain in the ass sometimes, but he's the heart of the party. Plus, he's got the basement with all our D&D gear. Total campaign master!'"
"eleven/el/jane"					who				"'El?' Dustin lowers his voice to a whisper. 'She's our real-life mage with psychic powers! And I mean REAL powers, not just role-playing stuff. She can flip vans with her mind! But don't tell anyone I told you that, seriously.'"
"lucas/sinclair"					who				"'Lucas is our party's ranger, and man, can that guy handle a wrist rocket!' Dustin grins. 'He's probably the most level-headed of us all - except when he's arguing with me, which is totally not my fault by the way. But he's always got our backs.'"
"will/byers"						who				"'Will the Wise! He's our cleric and probably the best artist in Hawkins,' Dustin says warmly. 'He's been through some serious shit - sorry, stuff - but he's tougher than anyone gives him credit for. Plus, his D&D campaigns are legendary!'"
"steve/harrington"					who				"'Steve The Hair Harrington?' Dustin grins widely. 'He's like my older brother, man. Started as kind of a douche - sorry, I mean, challenging individual - but now he's the best babysitter in Hawkins. Plus, he's got mad monster-fighting skills with that nail bat!'"
"max/maxine/mayfield"				who				"'Max? She's this totally rad California girl who can skateboard like a boss!' Dustin exclaims. 'She kicked our high scores' asses at the arcade, and she's basically become our party's zoomer. Plus, she puts up with Lucas, which is pretty impressive!'"
"nancy/wheeler"						who				"'Nancy? She's Mike's sister and a total badass!' Dustin says enthusiastically. 'She went from being this straight-A student to someone who can handle a shotgun like a pro. Plus, she's got some serious investigative skills. Like a real-life Barbara Gordon!'"
"jonathan/byers"					who				"'Jonathan is Will's older brother,' Dustin explains. 'He's kind of quiet, but in a cool way, you know? Amazing photographer, and he's got this killer music taste. Plus, he and Nancy make an unstoppable monster-hunting duo!'"
"hopper/jim"						who				"'Chief Hopper?' Dustin's eyes widen. 'Sure, he can be scary as hell when he's mad, but he's saved our butts more times than I can count. Plus, he's basically El's dad now, which is pretty awesome.'"
"joyce/byers"						who				"'Mrs. Byers? She's like the most determined mom in the universe!' Dustin says admiringly. 'When something's wrong with Will, she goes full mama bear mode. And let me tell you, NOTHING stops Joyce Byers when she's on a mission. Nothing!'"
"robin/buckley"						who				"'Robin? She used to work with Steve at the ice cream shop, and man, she was something else,' Dustin says, his voice softening. 'She could crack Soviet codes like it was second nature. But what stands out is how close she and Steve were—like real friends. They had each other's backs, no matter what. You don’t find that kind of friendship every day.'"
"eddie/munson"						who				"'Eddie? The leader of the Hellfire Club and the most metal D&D master you'll ever meet!' Dustin's voice fills with excitement, but there's a hint of sadness behind it. 'He was this totally rad guitarist, could shred on both an electric guitar AND with a 20-sided die. The guy was a legend! And... he was a hero, too. Eddie went out fighting for all of us, against things no one should ever have to face. He didn’t deserve to go like that, but we all owe him everything. He’ll always be a legend.'"
"dart/demodog"						who				"'Dart?' Dustin's voice gets quiet and a bit sad. 'He was... well, he was kind of my pet demodog. I know, I know, sounds crazy! I found him as a tiny pollywog. I actually thought he was just a weird lizard at first. Turned out to be way more complicated than that...'"
"dart/demodog"						what			"'Son of a bitch, that's a sensitive subject,' Dustin says, adjusting his hat nervously. 'Let's just say he started as a pollywog, grew into what I thought was a baby Demogorgon, but turned out to be what we call a demodog. And yeah, he had a thing for nougat.'"
"dart/demodog"						why				"'Why did I keep him?' Dustin sighs deeply. 'Because sometimes you want to believe something dangerous can be good, you know? Plus, he really seemed to like me. In the end, he even remembered our friendship - saved our lives by letting us pass in the tunnels!'"
"nougat/candy/3musketeers"			what			"'Are you kidding me? Only the greatest candy bar ever invented!' Dustin's eyes light up. 'Three Musketeers - pure nougaty goodness wrapped in chocolate. It's not just candy, it's a peace offering that works on both humans AND demodogs!'"
"nougat/candy/3musketeers"			why				"'Why is nougat so amazing?' Dustin looks offended at the question. 'First off, it's delicious. Second, it saved our lives once - no joke! And third, have you TRIED nougat? It's like a cloud of happiness covered in chocolate!'"
"suzie/suzie-poo"					who				"'Suzie?' Dustin's face breaks into a huge grin. 'Only the most brilliant girl in all of Utah! We met at science camp - Camp Know Where. She's got an IQ that's off the charts and she knows more about Planck's constant than anyone I've ever met, plus she is hotter than Pheobe Cates!'"
"suzie/suzie-poo"					what			"'What can I say about Suzie?' Dustin gets a dreamy look. 'She's my girlfriend, though she lives in Utah. Total genius, amazing singer - you should hear our duets! And she can recite more digits of Pi than anyone else at science camp!'"
"suzie/suzie-poo"					how				"'How did we meet?' Dustin straightens his cap proudly. 'At Camp Know Where - it's this super elite science camp. We bonded over our shared love of science, mathematics, and... well, let's just say we have amazing chemistry together!'"
"radio/walkietalkie/supercom"		what			"'This bad boy?' Dustin proudly holds up his radio. 'It's a Realistic TRC-214, top of the line! With some modifications, this baby can reach frequencies way beyond what Radio Shack intended. Perfect for interdimensional emergencies!'"
"radio/walkietalkie/supercom"		how				"'The secret is in the setup, my friend,' Dustin explains excitedly. 'You need the right crystals, proper antenna configuration, and enough juice to boost the signal. Plus, you gotta know Planck's constant - trust me, it comes in handy!'"
"radio/walkietalkie/supercom"		why				"'Why use radios? Because parents can hear us on our phones, my friend!' Dustin says with a knowing look. 'Plus, when you're dealing with supernatural emergencies, you need reliable and transportable communication."
"batteries/battery"					what			"'We're talking about the lifeblood of all our equipment!' Dustin pulls out a handful. 'Radio Shack's finest - when they're fresh, at least. Nothing worse than dead batteries during a code red situation!'"
"batteries/battery"					why				"'Why are batteries so important? Son of a bitch, don't even get me started!' Dustin exclaims. 'Try fighting interdimensional monsters with dead equipment! These babies keep our radios running and our lights shining.'"
"wrist rocket/slingshot"			what			"'Ah, the wrist rocket!' Dustin nods approvingly. 'Lucas's weapon of choice. Simple but effective - we're talking surgical precision here. Sure saved our asses - I mean, saved our butts - more than once!'"
"wrist rocket/slingshot"			how				"'The key is in the technique,' Dustin demonstrates with an invisible slingshot. 'Lucas has got it down to a science - calculate the trajectory, factor in wind resistance, and BAM! Direct hit! Though maybe don't tell him I was praising his skills.'"
"compass/compasses"					what			"'These aren't just your ordinary compasses,' Dustin says, tapping one significantly. 'They're our early warning system for supernatural activity. When the electromagnetic field goes wonky, these babies go crazy! It's basically like a poor man's interdimensional detector.'"
"compass/compasses"					how				"'The needle responds to magnetic fields, right?' Dustin explains, getting into teacher mode. 'So when there's interdimensional activity, the electromagnetic disruption makes them go haywire. Pretty scientific stuff!'"
"heathkit/ham radio"				what			"'The Heathkit?' Dustin's eyes light up. 'Only the most powerful ham radio setup in Hawkins! We're talking about reaching frequencies that would make Radio Shack employees weep. Perfect for reaching my girlfriend in Utah - when she's not grounded, that is.'"
"heathkit/ham radio"				how				"'First, you need to understand the basics of radio waves,' Dustin starts excitedly. 'Then it's all about finding the right frequency, adjusting for atmospheric conditions, and maybe singing a little NeverEnding Story duet... uh, forget that last part.'"
"cerebro"							what			"'CEREBRO!' Dustin shouts triumphantly. 'My masterpiece of radio engineering! Built it myself at the highest point in Hawkins. This baby can reach all the way to Utah with crystal clear reception. Well, when the weather cooperates, and when certain girlfriends aren't grounded.'"
"cerebro"							how				"'It's all about altitude, power, and precise calibration,' Dustin explains, sketching in the air. 'You need the highest point possible and proper antenna alignment."
"flashlight/lights"					what			"'Essential monster-hunting equipment!' Dustin declares, pulling out a flashlight. 'When you're exploring suspicious tunnels or tracking interdimensional creatures, you need reliable illumination. Plus, they're great for sending morse code in emergencies!'"
"backpack/supplies"					what			"'My emergency kit!' Dustin pats his backpack. 'Always packed with the essentials: compass, radio, spare batteries, emergency snacks - heavy on the nougat.'"
"paddle ball"						what			"'Ah, the paddle ball!' Dustin demonstrates with an invisible one. 'Perfect for testing hand-eye coordination and driving your friends crazy during long stakeouts. Plus, it helps pass the time while waiting for supernatural emergencies!'"
"escape/way out/getting out"			what			"'Listen up because this is crucial,' Dustin leans forward conspiratorially. 'There are two ways out of here. First, my compass and Lucas's wrist rocket together can tear open a portal. Or... there's the radio on channel 11, but that's playing with fire - literally dealing with Vecna himself.'"
"escape/way out/getting out"			how				"'Okay, here's the deal,' Dustin explains excitedly. 'Option one: find my compass - it's in that ghost trap over there - and Lucas's wrist rocket from the kitchen. Get them both, and BOOM, instant portal! Option two: the radio. Channel 11 might work, but you're basically inviting Vecna into your head. Your choice, but personally, I'd go for the items.'"
"portal/gate/dimensional tear"		what			"'The portal?' Dustin's eyes light up with scientific enthusiasm. 'It's a tear in the fabric of space-time, a bridge between dimensions! When it's active, it glows and pulses with electromagnetic energy. Getting it open though, that's the tricky part.'"
"portal/gate/dimensional tear"		how				"'There are two ways to activate it,' Dustin explains, counting on his fingers. 'First method: my compass and Lucas's wrist rocket together create enough electromagnetic disruption to force it open. Second method: channel 11 on that radio over there, but that means dealing with Vecna's psychic powers - not exactly ideal!'"
"time/hurry/rush"					what			"'Oh man, oh man,' Dustin checks his watch nervously. 'Listen, once the containment protocols kick in, that's it - you're stuck here forever! The clock's ticking, so either grab my compass and Lucas's wrist rocket, or risk the radio, but do something FAST!'"
"help/hint/stuck"					how				"'Son of a bitch, you need the crash course?' Dustin adjusts his cap. 'Okay, here's the 411: My compass is in the ghost trap, Lucas's wrist rocket is in the kitchen. Get both, portal opens. Or, tune that radio to channel 11. And whatever you do, don't waste time!'"

Chapter 11 - Alternate Death Pansed by Eleven

To respond to the question:
	repeat through the Table of Dustin's Answers:
		if the topic understood includes topic entry:
			if the current question is the question type entry:
				say "[reply entry][paragraph break]";
				rule succeeds;
	increment the stump-counter;
	if the stump-counter is 1:
		say "Dustin scratches his head: 'Son of a bitch, that's a tough one! Keep asking questions like that and I'll have to call Eleven. And trust me, you do NOT want that! You've got four more warnings'";
	otherwise if the stump-counter is 2:
		say "'Jesus Christ, another impossible question?' Dustin adjusts his hat nervously. 'Three more of these and El's gonna make you need a change of pants!'";
	otherwise if the stump-counter is 3:
		say "'Holy shit, you're really testing me here!' Dustin reaches for his radio. 'Two more stumpers and El's gonna give you a bathroom emergency!'";
	otherwise if the stump-counter is 4:
		say "'God damn it, you're really pushing it!' Dustin starts fiddling with his radio. 'One more weird question and you're gonna need a shower, my friend!'";
	otherwise if the stump-counter is 5:
		say "'SHIT! That's it!' Dustin grabs his radio. 'El, do your thing!' [paragraph break]Suddenly, you feel a strange pressure in your bladder. Your pants become mysteriously wet.[paragraph break]";
		end the story saying "You've been psychically pantsed by Eleven! You die of embarassment.".


Chapter 12 - Alternate Happy Ending Radio 

The radio is a device in the Living Room. The radio can be vecna-activated. The radio is not vecna-activated. The radio has a number called the channel. The channel of the radio is 5. The description is "An old radio covered in dust. In the eerie silence of the Upside Down, you wonder if it still works. There are two channels: 5 and 11. The radio is currently set to channel [channel of the radio]."

Instead of switching on the radio:
	if the radio is switched on:
		say "The radio is already on.";
	otherwise:
		if the channel of the radio is 11:
			now the radio is vecna-activated;
			now the radio is switched on;
			say "As you turn on the radio to channel 11, a haunting melody fills the room. Your favorite song... but how did it know? You feel a strange presence in your mind - Vecna! But wait... his psychic connection to you has actually created a stronger bridge between dimensions. The interdimensional tear above you pulses with strange energy, as though it is now open!";
			now the portal is active;
		otherwise:
			now the radio is switched on;
			say "You switch on the radio to channel [channel of the radio], but all you hear is static. It seems like this channel is not working."

Tuning it to is an action applying to one thing and one number.

Understand "change [something] to [number]" or "change [something] to channel [number]" or "tune [something] to [number]" or "tune [something] to channel [number]" as tuning it to.

Check tuning it to:
	if the noun is not the radio:
		say "That's not something you can tune." instead;
	if the number understood is not 5 and the number understood is not 11:
		say "The radio only has channels 5 and 11." instead.

Carry out tuning it to:
	if the number understood is 11:
		now the channel of the radio is 11;
		if the radio is switched on:
			now the radio is vecna-activated;
			now the portal is active;
			say "You tune the radio to channel 11, and the static fades. A haunting melody fills the room, and you feel a strange presence invade your mind - Vecna's psychic power. The interdimensional tear above you pulses with energy, as though it is now open!";
		otherwise:
			say "You tune the radio to channel 11. The radio is currently off.";
	if the number understood is 5:
		now the channel of the radio is 5;
		if the radio is switched on:
			say "You tune the radio to channel 5, but all you hear is static. The portal remains inactive.";
		otherwise:
			say "You tune the radio to channel 5. The radio is currently off."


Instead of entering the portal when the portal is inactive:
	say "The tear isn't active enough to pass through yet. You need to find Dustin's Compass and Lucas's Wristwrocket, or perhaps find another way to strengthen the dimensional tear."

Instead of entering the portal when the portal is active and the radio is vecna-activated:
	say "Using Vecna's own psychic connection against him, you focus your mind and leap 	through the portal. The world twists inside out around you...";
    	end the story finally saying "It turns out Vecna had you in a trance, you've escaped the Upside Down and Vecna's curse through the power of music!"

Instead of entering the portal when the portal is active and the radio is not vecna-activated:
	say "You brace yourself and leap through the crackling portal. The world twists inside out 	around you...";
    	end the story finally saying "You've escaped the Upside Down!"

Chapter 13 - Death Ending Radio on Too Long

The vecna-exposure is a number that varies. The vecna-exposure is 0.

Every turn:
	if the radio is switched on:
		increase the vecna-exposure by 1;
		if the vecna-exposure is 2:
			say "You feel a sharp pain behind your eyes...";
		if the vecna-exposure is 3:
			say "The noise from the radio is almost too much, maybe you should switch it off";
		if the vecna-exposure is 6:
			say "The noise on the radio reaches a crescendo. Your mind shatters as Vecna's full power overwhelms you. The last thing you see is a grotesque figure emerging from the shadows...";
			end the story saying "Vecna has claimed another victim. You should have moved faster."

Chapter 14 - Happy Ending Finding Two Objects

Every turn when the player has Dustin's compass and the player has Lucas's wrist rocket:
	if the portal is inactive:
		now the portal is active;
        	say "As you hold both items together, they vibrate strangely! The compass
		needle spins wildly, and the wrist rocket vibrates with energy. Suddenly, the magical 		portal springs to life, crackling with power!"










